#define NEWLINE 0x0a
#define SPACE 0x20
#define MAX_LINE_LENGTH 32

tracepoint:syscalls:sys_enter_read
/ comm == "cat" /
{
    if (*args->buf == 0) {
        return;
    }

    $i = 0;
    $num = 0;
    $prev = -1;
    $ascending = -1;

    while ($i < MAX_LINE_LENGTH) {
        $char = *(args->buf + $i);
        
        if ($char == SPACE || $char == NEWLINE) {
            if ($prev == -1) {
                $prev = $num;
            } else {
                if ($ascending == -1) {
                    $ascending = $num > $prev ? 1 : 0;
                }

                $diff = $num - $prev;
                if ($ascending && ($diff < 1 || $diff > 3)) {
                    return;
                }
                if (!$ascending && ($diff > -1 || $diff < -3)) {
                    return;
                }
            }

            if ($char == NEWLINE) {
                break;
            }

            $prev = $num;
            $num = 0;
        } else {
            $digit = $char - 48;
            $num = $num * 10 + $digit;
        }

        $i++;
    }

    @total++;
}

END
{
    print(@total);
}
